@PACKAGE_INIT@

if (NOT TARGET DLAF)
  include(${CMAKE_CURRENT_LIST_DIR}/DLAF-Targets.cmake)
endif()

# enable custom modules to be used
set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

# ===== VARIABLES
set(DLAF_WITH_MKL @DLAF_WITH_MKL@)

# ===== DEPENDENCIES
include(CMakeFindDependencyMacro)

# ----- MPI
find_dependency(MPI REQUIRED)

# ----- LAPACK
if (DLAF_WITH_MKL)
  set(MKL_ROOT "@MKL_ROOT@")
  set(MKL_CUSTOM_THREADING "@MKL_THREADING@")

  set(MKL_LAPACK_CUSTOM_INCLUDE_DIR "@MKL_LAPACK_INCLUDE_DIR@")
  set(MKL_LAPACK_CUSTOM_LIBRARY "@MKL_LAPACK_LIBRARY@")

  find_dependency(MKL REQUIRED COMPONENTS LAPACK)
else()
  set(LAPACK_CUSTOM_TYPE "@LAPACK_TYPE@")
  set(LAPACK_CUSTOM_INCLUDE_DIR "@LAPACK_INCLUDE_DIR@")
  set(LAPACK_CUSTOM_LIBRARY "@LAPACK_LIBRARY@")

  find_dependency(LAPACK REQUIRED)
endif()

# ----- HPX
find_dependency(HPX REQUIRED PATHS @HPX_DIR@)

check_required_components(DLAF)
