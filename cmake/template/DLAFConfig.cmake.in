#
# Distributed Linear Algebra with Future (DLAF)
#
# Copyright (c) 2018-2024, ETH Zurich
# All rights reserved.
#
# Please, refer to the LICENSE file in the root directory.
# SPDX-License-Identifier: BSD-3-Clause
#

@PACKAGE_INIT@

if(NOT TARGET DLAF)
  include(${CMAKE_CURRENT_LIST_DIR}/DLAF-Targets.cmake)
endif()

# enable custom modules to be used
list(PREPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

# ===== VARIABLES
set(DLAF_WITH_OPENMP @DLAF_WITH_OPENMP@)
set(DLAF_WITH_CUDA @DLAF_WITH_CUDA@)
set(DLAF_WITH_HIP @DLAF_WITH_HIP@)
set(DLAF_WITH_GPU @DLAF_WITH_GPU@)
set(DLAF_WITH_SCALAPACK @DLAF_WITH_SCALAPACK@)
set(DLAF_WITH_HDF5 @DLAF_WITH_HDF5@)

# ===== DEPENDENCIES
include(CMakeFindDependencyMacro)

# ---- CUDA/HIP
if(DLAF_WITH_CUDA)
  find_dependency(CUDAToolkit)
endif()

if(DLAF_WITH_HIP)
  find_dependency(rocblas)
  find_dependency(rocsolver)
endif()

if(DLAF_WITH_GPU)
  find_dependency(whip)
endif()

# ----- MPI
find_dependency(MPI)

# ----- OpenMP
if(DLAF_WITH_OPENMP)
  find_dependency(OpenMP)
endif()

# ----- LAPACK
set(LAPACK_LIBRARY "@LAPACK_LIBRARY@")
set(LAPACK_INCLUDE_DIR "@LAPACK_INCLUDE_DIR@")
find_dependency(LAPACK)

# ----- ScaLAPACK
if(DLAF_WITH_SCALAPACK)
  set(SCALAPACK_LIBRARY "@SCALAPACK_LIBRARY@")
  set(SCALAPACK_INCLUDE_DIR "@SCALAPACK_INCLUDE_DIR@")
  find_dependency(SCALAPACK)
endif()

# ----- pika
find_dependency(pika PATHS @pika_DIR@)

# ----- BLASPP/LAPACKPP
find_dependency(blaspp PATHS @blaspp_DIR@)
find_dependency(lapackpp PATHS @lapackpp_DIR@)

# ----- UMPIRE
find_dependency(Umpire PATHS @Umpire_DIR@)

if(DLAF_WITH_HDF5)
  find_dependency(HDF5 PATHS @HDF5_DIR@)
endif()

check_required_components(DLAF)
